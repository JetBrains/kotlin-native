/*
 * Copyright 2010-2019 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license
 * that can be found in the LICENSE file.
 */
import org.jetbrains.kotlin.CompileToBitcode
import org.jetbrains.kotlin.ClangFormatFixTask
import org.jetbrains.kotlin.ClangFormatCheckTask
import org.jetbrains.kotlin.UtilsKt

// TODO: consider using some Gradle plugins to build and test

void includeRuntime(CompileToBitcode task) {
    task.compilerArgs.add('-I' + project.file('../common/src/hash/headers'))
    task.compilerArgs.add('-I' + project.file('src/main/cpp'))
}

targetList.each { targetName ->
    tasks.create("${targetName}Runtime", CompileToBitcode, file('src/main'), "runtime", targetName).configure {
        dependsOn ":common:${targetName}Hash"
        dependsOn "${targetName}StdAlloc"
        dependsOn "${targetName}OptAlloc"
        dependsOn "${targetName}Mimalloc"
        dependsOn "${targetName}Launcher"
        dependsOn "${targetName}Debug"
        dependsOn "${targetName}Release"
        dependsOn "${targetName}Strict"
        dependsOn "${targetName}Relaxed"
        dependsOn "${targetName}ProfileRuntime"
        dependsOn "${targetName}ObjC"
        dependsOn "${targetName}ExceptionsSupport"
        includeRuntime(delegate)
        linkerArgs.add(project.file("../common/build/$targetName/hash.bc").path)
    }

    tasks.create("${targetName}Mimalloc", CompileToBitcode, file('src/mimalloc'), "mimalloc", targetName).configure {
        language = CompileToBitcode.Language.C
        excludeFiles.addAll(["**/alloc-override*.c", "**/page-queue.c", "**/static.c"])
        if (!UtilsKt.targetSupportsMimallocAllocator(targetName))
            excludedTargets.add(targetName)
        srcDir = new File(srcRoot, "c")
        compilerArgs.add("-DKONAN_MI_MALLOC=1")
        headersDir = new File(srcDir, "include")
    }

    tasks.create("${targetName}Launcher", CompileToBitcode, file('src/launcher'), "launcher", targetName).configure {
        includeRuntime(delegate)
    }

    tasks.create("${targetName}Debug", CompileToBitcode, file('src/debug'), "debug", targetName).configure {
        includeRuntime(delegate)
    }

    tasks.create("${targetName}StdAlloc", CompileToBitcode, file('src/std_alloc'), "std_alloc", targetName)

    tasks.create("${targetName}OptAlloc", CompileToBitcode, file('src/opt_alloc'), "opt_alloc", targetName)

    tasks.create("${targetName}ExceptionsSupport", CompileToBitcode, file('src/exceptions_support'),
            "exceptionsSupport", targetName).configure {
        includeRuntime(delegate)
    }

    tasks.create("${targetName}Release", CompileToBitcode, file('src/release'), "release", targetName).configure {
        includeRuntime(delegate)
    }

    tasks.create("${targetName}Strict", CompileToBitcode, file('src/strict'), "strict", targetName).configure {
        includeRuntime(delegate)
    }

    tasks.create("${targetName}Relaxed", CompileToBitcode, file('src/relaxed'), "relaxed", targetName).configure {
        includeRuntime(delegate)
    }

    tasks.create("${targetName}ProfileRuntime", CompileToBitcode, file('src/profile_runtime'),
            "profileRuntime", targetName)

    tasks.create("${targetName}ObjC", CompileToBitcode, file('src/objc'), "objc", targetName).configure {
        includeRuntime(delegate)
    }
}

task hostRuntime(dependsOn: "${hostName}Runtime")

task clean {
    doLast {
        delete buildDir
    }
}

task generateJsMath {
    dependsOn ':distCompiler'
    doLast {
        def jsinteropScript = isWindows() ? "jsinterop.bat" : "jsinterop"
        def jsinterop = "$distDir/bin/$jsinteropScript"
        def targetDir = "$buildDir/generated"
        "$jsinterop -pkg kotlinx.interop.wasm.math -o $targetDir/math -target wasm32".execute().waitFor()
        def generated = file("$targetDir/math-build/natives/js_stubs.js")
        def mathJs = file('src/main/js/math.js')
        mathJs.write("// NOTE: THIS FILE IS AUTO-GENERATED!\n" +
                     "// Run ':runtime:generateJsMath' to re-generate it.\n\n")
        generated.withReader {
            mathJs.append(it)
        }
    }
}

def clangFormatTargets = project.fileTree("src/main") {
    include '**/*.c'
    include '**/*.cpp'
    include '**/*.h'
    include '**/*.hpp'
    include '**/*.m'
    include '**/*.mm'

    exclude 'cpp/utf8.h' // part of external library

    // TODO: Reformat all these files.
    exclude 'cpp/Alloc.h'
    exclude 'cpp/Arrays.cpp'
    exclude 'cpp/Atomic.cpp'
    exclude 'cpp/Atomic.h'
    exclude 'cpp/Boxing.cpp'
    exclude 'cpp/Common.h'
    exclude 'cpp/Console.cpp'
    exclude 'cpp/CyclicCollector.cpp'
    exclude 'cpp/DoubleConversions.h'
    exclude 'cpp/Exceptions.cpp'
    exclude 'cpp/Exceptions.h'
    exclude 'cpp/ExecFormat.cpp'
    exclude 'cpp/ExecFormat.h'
    exclude 'cpp/Interop.cpp'
    exclude 'cpp/JSInterop.cpp'
    exclude 'cpp/KAssert.cpp'
    exclude 'cpp/KAssert.h'
    exclude 'cpp/KDebug.h'
    exclude 'cpp/KString.cpp'
    exclude 'cpp/KString.h'
    exclude 'cpp/KotlinMath.cpp'
    exclude 'cpp/KotlinMath.h'
    exclude 'cpp/Memory.cpp'
    exclude 'cpp/Memory.h'
    exclude 'cpp/MemoryPrivate.hpp'
    exclude 'cpp/MemorySharedRefs.cpp'
    exclude 'cpp/MemorySharedRefs.hpp'
    exclude 'cpp/Natives.cpp'
    exclude 'cpp/Natives.h'
    exclude 'cpp/ObjCExceptions.cpp'
    exclude 'cpp/ObjCExceptions.h'
    exclude 'cpp/ObjCExport.h'
    exclude 'cpp/ObjCExport.mm'
    exclude 'cpp/ObjCExportCollectionUtils.mm'
    exclude 'cpp/ObjCExportCollections.h'
    exclude 'cpp/ObjCExportCoroutines.mm'
    exclude 'cpp/ObjCExportErrors.h'
    exclude 'cpp/ObjCExportErrors.mm'
    exclude 'cpp/ObjCExportInit.h'
    exclude 'cpp/ObjCExportPrivate.h'
    exclude 'cpp/ObjCInterop.cpp'
    exclude 'cpp/ObjCInterop.h'
    exclude 'cpp/ObjCInteropUtils.mm'
    exclude 'cpp/ObjCInteropUtilsPrivate.h'
    exclude 'cpp/Operator.cpp'
    exclude 'cpp/Porting.cpp'
    exclude 'cpp/Porting.h'
    exclude 'cpp/PthreadUtils.cpp'
    exclude 'cpp/PthreadUtils.h'
    exclude 'cpp/Regex.cpp'
    exclude 'cpp/ReturnSlot.cpp'
    exclude 'cpp/Runtime.cpp'
    exclude 'cpp/Runtime.h'
    exclude 'cpp/SourceInfo.h'
    exclude 'cpp/StdCppStubs.cpp'
    exclude 'cpp/ToString.cpp'
    exclude 'cpp/TypeInfo.cpp'
    exclude 'cpp/TypeInfo.h'
    exclude 'cpp/Types.cpp'
    exclude 'cpp/Types.h'
    exclude 'cpp/Utils.h'
    exclude 'cpp/Weak.cpp'
    exclude 'cpp/Worker.cpp'
    exclude 'cpp/Worker.h'
}

task format(type: ClangFormatFixTask) {
    files = clangFormatTargets
}

task formatCheck(type: ClangFormatCheckTask) {
    files = clangFormatTargets
}
