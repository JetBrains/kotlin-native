apply plugin: 'konan'

konanInterop {
    stdio {
        defFile 'stdio.def'
    }
}

konanArtifacts {
    CsvParser {
        inputFiles project.file('CsvParser.kt')
        useInterop 'stdio'
    }
}

build {
    project.ext { outputFile = "${projectDir.canonicalPath}/${file(compileKonanCsvParser.artifactPath).name}" }
    doLast {
        copy {
            from compileKonanCsvParser.artifactPath
            into projectDir.canonicalPath
        }
    }
}

void dumpCompilationTask(Task task) {
    println()
    println("Compilation task: ${task.name}")
    println("outputDir          : ${task.outputDir}")
    println("artifactPath       : ${task.artifactPath}")
    println("inputFiles         : ${task.inputFiles.files}")
    println("libraries          : ${task.libraries}")
    println("nativeLibraries    : ${task.nativeLibraries}")
    println("linkerOpts         : ${task.linkerOpts}")
    println("noStdLib           : ${task.noStdLib}")
    println("noLink             : ${task.noLink}")
    println("noMain             : ${task.noMain}")
    println("enableOptimization : ${task.enableOptimization}")
    println("enableAssertions   : ${task.enableAssertions}")
    println("target             : ${task.target}")
    println("languageVersion    : ${task.languageVersion}")
    println("apiVersion         : ${task.apiVersion}")
}

void dumpInteropTask(Task task) {
    println()
    println("Stub generation task: ${task.name}")
    println("stubsDir           : ${task.stubsDir}")
    println("libsDir            : ${task.libsDir}")
    println("defFile            : ${task.defFile}")
    println("target             : ${task.target}")
    println("pkg                : ${task.pkg}")
    println("linker             : ${task.linker}")
    println("compilerOpts       : ${task.compilerOpts}")
    println("linkerOpts         : ${task.linkerOpts}")
    println("headers            : ${task.headers.files}")
    println("linkFiles          : ${task.linkFiles}")
}

task dumpParameters(type: DefaultTask) {
    doLast {
        dumpCompilationTask(konanArtifacts['CsvParser'].compilationTask)
        dumpInteropTask(konanInterop['stdio'].generateStubsTask)
        dumpCompilationTask(konanInterop['stdio'].compileStubsTask)
    }
}

clean {
    doLast {
        delete outputFile
    }
}