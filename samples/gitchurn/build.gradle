plugins {
    id 'kotlin-multiplatform'
}

// Determine host preset.
def hostPreset = MPPTools.defaultHostPreset(project)

kotlin {
    targets {
        fromPreset(hostPreset, 'gitChurn') {
            compilations.main.outputKinds 'EXECUTABLE'
            compilations.main.entryPoint 'sample.gitchurn.main'
            compilations.main.cinterops {
                libgit2 {
                    switch (hostPreset) {
                        case presets.macosX64:
                            includeDirs.headerFilterOnly '/opt/local/include', '/usr/local/include'
                            break
                        case presets.linuxX64:
                            includeDirs.headerFilterOnly '/usr/include'
                            break
                        case presets.mingwX64:
                            includeDirs.headerFilterOnly "${MPPTools.mingwPath()}/include"
                            break
                    }
                }
            }
            if (presets.mingwX64 == hostPreset) {
                compilations.main.linkerOpts "-L${MPPTools.mingwPath()}/lib"
            }
        }
    }
}

MPPTools.createRunTask(project, 'runProgram', kotlin.targets.gitChurn) {
    if (kotlin.presets.mingwX64 == hostPreset) {
        environment 'PATH': "${MPPTools.mingwPath()}/bin"
    }
    args project.getRootProject().getRootDir().toString() + '/..'
}
