plugins {
    id 'kotlin-multiplatform'
}

// Determine host preset.
def hostPreset = MPPTools.defaultHostPreset(project)

// Add two additional presets for Raspberry Pi.
def raspberryPiPresets = [kotlin.presets.linuxArm32Hfp, kotlin.presets.linuxArm64]

kotlin {
    targetFromPreset(hostPreset, 'echoServer') {
        binaries {
            executable() {
                entryPoint = 'sample.echoserver.main'
                runTask?.args(3000)
            }
        }
    }

    raspberryPiPresets.each { preset ->
        def targetName = 'echoServer' + preset.name.capitalize()
        targetFromPreset(preset, targetName) {
            binaries {
                executable() {
                    entryPoint = 'sample.echoserver.main'
                    runTask?.args(3000)
                }
            }
        }
    }

    sourceSets {
        raspberryPiPresets.each { preset ->
            String mainSourceSetName = 'echoServer' + preset.name.capitalize() + 'Main'
            getByName(mainSourceSetName) dependsOn echoServerMain
        }
    }
}
