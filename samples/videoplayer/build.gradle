apply plugin: 'org.jetbrains.kotlin.platform.native'

components.main {
    targets = ['macos_x64', 'linux_x64', 'mingw_x64']
    outputKinds = [EXECUTABLE]
    baseName = 'Player'

    dependencies {
        cinterop('ffmpeg') {
            target 'macos_x64', {
                includeDirs.headerFilterOnly '/opt/local/include', '/usr/local/include'
            }

            target 'linux_x64', {
                includeDirs.headerFilterOnly '/usr/include', '/usr/include/x86_64-linux-gnu', '/usr/include/ffmpeg'
            }

            target 'mingw_x64', {
                includeDirs 'C:/msys64/mingw64/include'
            }
        }

        cinterop ('sdl') {
            target 'macos_x64', {
                includeDirs '/Library/Frameworks/SDL2.framework/Headers',
                        "${System.getProperty("user.home")}/Library/Frameworks/SDL2.framework/Headers",
                        '/opt/local/include/SDL2',
                        '/usr/local/include/SDL2'
            }

            target 'linux_x64', {
                includeDirs '/usr/include/SDL2'
            }

            target 'mingw_x64', {
                includeDirs 'C:/msys64/mingw64/include/SDL2'
            }
        }
    }

    target 'macos_x64', {
        linkerOpts "-F ${System.getProperty("user.home")}/Library/Frameworks", "-F /Library/Frameworks",
                "-L/opt/local/lib", "-L/usr/local/lib"
    }

    target 'linux_x64', {
        linkerOpts '-L/usr/lib/x86_64-linux-gnu', '-L/usr/lib64'
    }

    target 'mingw_x64', {
        linkerOpts '-LC:/msys64/mingw64/lib'
    }
}

