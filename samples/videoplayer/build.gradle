plugins {
    id 'kotlin-multiplatform'
}

kotlin {
    presets {
        // Determine host preset.
        MPPTools.defaultHostPreset(project, [macosX64, linuxX64, mingwX64])
    }

    targets {
        fromPreset(hostPreset, 'videoPlayer') {
            compilations.main.outputKinds 'EXECUTABLE'
            compilations.main.entryPoint 'sample.videoplayer.main'

            switch (hostPreset) {
                case presets.macosX64:
                    compilations.main.linkerOpts "-F ${MPPTools.localFrameworksPath()}",
                            "-F ${MPPTools.systemFrameworksPath()}",
                            '-L/opt/local/lib',
                            '-L/usr/local/lib'
                    break
                case presets.linuxX64:
                    compilations.main.linkerOpts '-L/usr/lib/x86_64-linux-gnu', '-L/usr/lib64'
                    break
                case presets.mingwX64:
                    compilations.main.linkerOpts "-L${MPPTools.mingwPath()}/lib"
                    break
            }

            compilations.main.cinterops {
                ffmpeg {
                    switch (hostPreset) {
                        case presets.macosX64: 
                            includeDirs.headerFilterOnly '/opt/local/include', '/usr/local/include'
                            break
                        case presets.linuxX64:
                            includeDirs.headerFilterOnly '/usr/include', '/usr/include/x86_64-linux-gnu', '/usr/include/ffmpeg'
                            break
                        case presets.mingwX64:
                            includeDirs "${MPPTools.mingwPath()}/include"
                            break
                    }
                }
                sdl {
                    switch (hostPreset) {
                        case presets.macosX64:
                            includeDirs "${MPPTools.localFrameworksPath()}/SDL2.framework/Headers",
                                    "${MPPTools.systemFrameworksPath()}/SDL2.framework/Headers",
                                    '/opt/local/include/SDL2',
                                    '/usr/local/include/SDL2'
                            break
                        case presets.linuxX64:
                            includeDirs '/usr/include/SDL2'
                            break
                        case presets.mingwX64:
                            includeDirs "${MPPTools.mingwPath()}/include/SDL2"
                            break
                    }
                }
            }
        }
    }
}
