/*
 * Copyright 2010-2017 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import groovy.io.FileType
import org.jetbrains.kotlin.konan.target.*
import org.jetbrains.kotlin.konan.properties.*
import org.jetbrains.kotlin.konan.util.*
import static org.jetbrains.kotlin.konan.util.VisibleNamedKt.*

defaultTasks 'clean', 'dist'

convention.plugins.platformInfo = new PlatformInfo()

ext {
    konanPropertiesFile = project(':backend.native').file('konan.properties')
    konanProperties = PropertiesKt.loadProperties(konanPropertiesFile.absolutePath)

    String DEPENDENCY_PROFILES_KEY = "dependencyProfiles"
    def dependencyProfiles = konanProperties.getProperty(DEPENDENCY_PROFILES_KEY)
    if (dependencyProfiles != "default alt")
        throw new Error("unexpected $DEPENDENCY_PROFILES_KEY value: expected 'default alt', got '$dependencyProfiles'")

    // Force build to use only 'default' profile:
    konanProperties.setProperty(DEPENDENCY_PROFILES_KEY, "default")
    // TODO: it actually affects only resolution made in :dependencies,
    // that's why we assume that 'default' profile comes first (and check this above).

    distDir = file('dist')
    dependenciesDir = DependencyProcessor.defaultDependenciesRoot
    clangManager = new ClangManager(konanProperties, dependenciesDir.absolutePath)
    kotlinCompilerModule="org.jetbrains.kotlin:kotlin-compiler:${kotlinCompilerVersion}"
    kotlinStdLibModule="org.jetbrains.kotlin:kotlin-stdlib:${kotlinStdLibVersion}"
    kotlinReflectModule="org.jetbrains.kotlin:kotlin-reflect:${kotlinReflectVersion}"
    kotlinScriptRuntimeModule="org.jetbrains.kotlin:kotlin-script-runtime:${kotlinScriptRuntimeVersion}"
}

allprojects {
    if (path != ":dependencies") {
        evaluationDependsOn(":dependencies")
    }

    repositories {
        mavenCentral()
        maven {
            url kotlinCompilerRepo
        }
    }

    setupHostAndTarget()
    loadCommandLineProperties()
    loadLocalProperties()
    setupClang(project)
}

void setupHostAndTarget() {
    ext.hostName = TargetManager.hostName
    ext.targetList = TargetManager.enabled.collect { getVisibleName(it) } as List
}

void setupClang(Project project) {

    project.convention.plugins.clangManager = project.rootProject.ext.clangManager
    project.convention.plugins.execClang = new org.jetbrains.kotlin.ExecClang(project)

    project.plugins.withType(NativeComponentPlugin) {
        project.model {
            if (isWindows()) {
                platforms {
                    host {
                        architecture 'x86_64'
                    }
                }

                components {
                    withType(NativeComponentSpec) {
                        targetPlatform 'host'
                    }
                }

                toolChains {
                    gcc(Gcc) {
                        path "$llvmDir/bin"
                    }
                }
            } else {

                toolChains {
                    clang(Clang) {
                        hostClang.hostClangPath.each {
                            path it
                        }

                        eachPlatform { // TODO: will not work when cross-compiling
                            [cCompiler, cppCompiler, linker].each {
                                it.withArguments { it.addAll(project.hostClangArgs) }
                            }

                        }
                    }
                }
            }
        }
    }
}

void loadLocalProperties() {
    if (new File("$project.rootDir/local.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream("$project.rootDir/local.properties"))
        props.each { prop -> project.ext.set(prop.key, prop.value) }
    }
}

void loadCommandLineProperties() {
    if (project.hasProperty("konanc_flags")) {
        throw new Error("Specify either -Ptest_flags or -Pbuild_flags.")
    }
    ext.globalBuildArgs = project.hasProperty("build_flags") ? ext.build_flags.split() : []
    ext.globalTestArgs = project.hasProperty("test_flags") ? ext.test_flags.split() : []
    ext.testTarget = project.hasProperty("test_target") ? ext.test_target : null
}

class PlatformInfo {
    boolean isMac() {
        return TargetManager.host == KonanTarget.MACBOOK
    }

    boolean isWindows() {
        return TargetManager.host == KonanTarget.MINGW
    }

    boolean isLinux() {
        return TargetManager.host == KonanTarget.LINUX
    }

    Throwable unsupportedPlatformException() {
        return new TargetSupportException()
    }
}

task sharedJar {
    dependsOn gradle.includedBuild('kotlin-native-shared').task(':jar')
}

task gradlePluginJar {
    dependsOn gradle.includedBuild('kotlin-native-gradle-plugin').task(':shadowJar')
}

task gradlePluginCheck() {
    dependsOn gradle.includedBuild('kotlin-native-gradle-plugin').task(':check')
}

task gradlePluginUpload {
    dependsOn gradle.includedBuild('kotlin-native-gradle-plugin').task(':bintrayUpload')
}

task dist_compiler(dependsOn: "distCompiler")
task dist_runtime(dependsOn: "distRuntime")
task cross_dist(dependsOn: "crossDist")
task list_dist(dependsOn: "listDist")

task distCompiler(type: Copy) {
    dependsOn ':backend.native:jar'
    dependsOn ':utilities:jar'
    dependsOn ':klib:jar'
    dependsOn ':sharedJar'

    destinationDir distDir

    from(project(':backend.native').file('build/libs')) {
        into('konan/lib')
    }

    from(project('Interop').file('Runtime/build/libs')) {
        into('konan/lib')
    }

    from(project('Interop').file('Indexer/build/libs')) {
        into('konan/lib')
    }

    from(project('Interop').file('StubGenerator/build/libs')) {
        into('konan/lib')
    }

    from(project(':backend.native').file('build/external_jars')) {
        into('konan/lib')
    }

    from(project(':backend.native').file("build/nativelibs/$hostName")) {
        into('konan/nativelib')
    }

    from(project(':Interop').file('Indexer/build/nativelibs')) {
        into('konan/nativelib')
    }

    from(project(':Interop').file('Runtime/build/nativelibs')) {
        into('konan/nativelib')
    }

    from(project(':llvmDebugInfoC').file('build/libs/debugInfo/shared')) {
        into('konan/nativelib')
    }

    from(project(':llvmDebugInfoC').file('src/scripts/konan_lldb.py')) {
        into('tools')
    }

    from(project(':utilities').file('env_blacklist')) {
        into('tools')
    }

    from(project(':utilities').file('build/libs')) {
        into('konan/lib')
    }

    from(project(':klib').file('build/libs')) {
        into('konan/lib')
    }

    from(file("${gradle.includedBuild('kotlin-native-shared').projectDir}/build/libs")) {
        into('konan/lib')
    }

    from(file('cmd')) {
        fileMode(0755)
        into('bin')
        if (!isWindows()) {
            exclude('**/*.bat')
        }
    }
    from(konanPropertiesFile) {
        into('konan')
    }
}

task listDist(type: Exec) {
    commandLine 'find', distDir
}

task distRuntime(type: Copy) {
    dependsOn "${hostName}CrossDistRuntime"
    dependsOn('commonDistRuntime')
}

def stdlib = 'klib/common/stdlib'

task commonDistRuntime(type: Copy) {
    destinationDir distDir

    // Target independant common part.
    from(project(':runtime').file("build/${hostName}Stdlib")) {
        include('**')
        into(stdlib)
    }
}

task crossDistRuntime(type: Copy) {
    dependsOn.addAll(targetList.collect { "${it}CrossDistRuntime" })
    dependsOn('commonDistRuntime')
}

task crossDistPlatformLibs {
    dependsOn.addAll(targetList.collect { "${it}PlatformLibs" })
}

targetList.each { target ->
    task("${target}CrossDistRuntime", type: Copy) {
        dependsOn ":runtime:${target}Runtime"
        dependsOn ":backend.native:${target}Stdlib"
        dependsOn ":backend.native:${target}Start"

        destinationDir distDir

        from(project(':runtime').file("build/$target")) {
            include("runtime.bc")
            into("$stdlib/targets/$target/native")
        }
        from(project(':runtime').file("build/$target")) {
            include("*.bc")
            exclude("runtime.bc")
            into("konan/targets/$target/native")
        }
        from(project(':runtime').file("build/${target}Stdlib")) {
            include('**')
            into(stdlib)
        }
        from(project(':runtime').file("build/${target}Start.bc")) {
            rename("${target}Start.bc", 'start.bc')
            into("konan/targets/$target/native")
        }
        if (target == 'wasm32') {
            from(project(':runtime').file('src/launcher/js')) {
                into("$stdlib/targets/wasm32/included")
            }
        }
    }

    task("${target}PlatformLibs") {
        dependsOn ":platformLibs:${target}Install"
    }

    task("${target}CrossDist") {
        dependsOn "${target}CrossDistRuntime", 'distCompiler', 'commonDistRuntime'
    }
}

task distPlatformLibs {
    dependsOn ':platformLibs:hostInstall'
}

task dist {
    dependsOn 'distCompiler', 'distRuntime'
}

task crossDist {
    dependsOn 'crossDistRuntime', 'distCompiler'
}

configurations {
    ftpAntTask
}

dependencies {
    ftpAntTask 'org.apache.ant:ant-commons-net:1.9.9'
}

task bundle(type: (isWindows()) ? Zip : Tar) {
    dependsOn('crossDistPlatformLibs')
    dependsOn('crossDist')
    def simpleOsName = TargetManager.simpleOsName()
    baseName = "kotlin-native-$simpleOsName-${project.konanVersion}"
    from("$project.rootDir/dist") {
        include '**'
        exclude 'dependencies'
        exclude 'klib/testLibrary'
        into baseName
    }
    from(project.rootDir) {
        include 'DISTRO_README.md'
        rename {
            return "README.md"
        }
        into baseName
    }
    from(project.rootDir) {
        include 'INTEROP.md'
        include 'RELEASE_NOTES.md'
        include 'GRADLE_PLUGIN.md'
        include 'PLATFORM_LIBS.md'
        include 'samples/**'
        exclude 'samples/gradle.properties'
        exclude 'samples/settings.gradle'
        exclude '**/build'
        exclude '**/.gradle'
        exclude 'samples/**/*.kt.bc-build'
        into baseName
    }

    from(project.file("samples/settings.gradle")) {
        into "$baseName/samples"
        filter { it.startsWith("includeBuild") ? null : it }
    }

    from(project.file("samples/gradle.properties")) {
        into "$baseName/samples"
        filter { it.startsWith("konan.home=") ? "konan.home=../.." : it }
    }

    destinationDir = file('.')
    if (!isWindows()) {
        extension = 'tar.gz'
        compression = Compression.GZIP
    }
}

task uploadBundle {
    dependsOn ':bundle'
    doLast {
        def kind = konanVersion.contains("dev") ? "dev" : "releases"
        def ftpSettings = [
                server: project.findProperty("cdnUrl") ?: System.getenv("CDN_URL"),
                userid: project.findProperty("cdnUser") ?: System.getenv("CDN_USER"),
                password: project.findProperty("cdnPass") ?: System.getenv("CDN_PASS"),
                remoteDir: "/builds/$kind/$konanVersion/${TargetManager.simpleOsName()}"
        ]
        ant {
            taskdef(name: 'ftp',
                    classname: 'org.apache.tools.ant.taskdefs.optional.net.FTP',
                    classpath: configurations.ftpAntTask.asPath)
            ftp([action: "mkdir"] + ftpSettings)
            ftp(ftpSettings) {
                fileset(file: bundle.archivePath)
            }
        }
    }
}

task performance {
    dependsOn 'dist'
    dependsOn ':performance:bench'
}

task clean {
    doLast {
        delete distDir
        delete bundle.outputs.files
    }
}

// Don't run with other tasks (e.g. ./gradlew cleanAll bundle)
task cleanAll {
    dependsOn gradle.includedBuilds.collect { it.task(":clean") }
    dependsOn project.getTasksByName("clean", true)
}
